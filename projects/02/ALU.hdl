// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    //// Replace this comment with your code.
    Mux16(a=x, b = false, sel=zx, out=outzx);
    Not16(in=outzx, out=outnzx);
    Mux16(a=outzx, b = outnzx, sel=nx, out=xoutput);
    
    Mux16(a=y, b = false, sel=zy, out=outzy);
    Not16(in=outzy, out=outnzy);
    Mux16(a=outzy, b = outnzy, sel=ny, out=youtput);

    Add16(a=xoutput, b = youtput, out = outAddxy);
    And16(a=xoutput, b = youtput, out = outAndxy);
    Mux16(a = outAndxy, b = outAddxy, sel =f, out = fout);
    Not16(in = fout, out = notfout);
    Mux16(a = fout, b = notfout, sel = no, out = out, out[0..7]=outA, out[8..15]=outB, out[15]=ng);
    Or8Way(in=outA, out=orA);
    Or8Way(in=outB, out=orB);
    Or(a=orA, b = orB, out=isNonZero);
    Not(in=isNonZero, out =zr);
}